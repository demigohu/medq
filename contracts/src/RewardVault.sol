// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import {Ownable} from "@openzeppelin/contracts/access/Ownable.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

import {MedqToken} from "./tokens/MedqToken.sol";

/**
 * @title RewardVault
 * @notice Holds MEDQ ERC-20 reward supply. The vault mints MEDQ tokens
 *         when quests are funded and distributes rewards to quest completers.
 */
contract RewardVault is Ownable {
    using SafeERC20 for IERC20;

    struct QuestReward {
        uint256 totalAmount;
        uint256 claimedAmount;
    }

    mapping(uint256 questId => QuestReward) private _rewards;

    address public questManager;
    MedqToken public medqToken;

    event QuestManagerUpdated(address indexed newQuestManager);
    event MedqTokenUpdated(address indexed tokenAddress);
    event RewardFunded(uint256 indexed questId, uint256 amount);
    event RewardReleased(uint256 indexed questId, address indexed recipient, uint256 amount);

    error RewardVault__OnlyQuestManager();
    error RewardVault__InvalidQuestManager();
    error RewardVault__QuestAlreadyFunded(uint256 questId);
    error RewardVault__InsufficientBalance(uint256 questId, uint256 requested, uint256 available);
    error RewardVault__MedqTokenNotConfigured();
    error RewardVault__QuestNotFunded(uint256 questId);

    modifier onlyQuestManager() {
        _requireQuestManager();
        _;
    }

    constructor(address owner_) Ownable(owner_) {}

    function setQuestManager(address questManager_) external onlyOwner {
        if (questManager_ == address(0)) revert RewardVault__InvalidQuestManager();
        questManager = questManager_;
        emit QuestManagerUpdated(questManager_);
    }

    function setMedqToken(address tokenAddress) external onlyOwner {
        if (tokenAddress == address(0)) revert RewardVault__MedqTokenNotConfigured();
        medqToken = MedqToken(tokenAddress);
        emit MedqTokenUpdated(tokenAddress);
    }

    /**
     * @notice Fund a quest with MEDQ tokens. Mints tokens to this vault.
     *         Called by QuestManager when a quest is created.
     * @param questId ID of the quest to fund
     * @param amount Total amount of MEDQ tokens to fund (with 18 decimals)
     */
    function fundQuest(uint256 questId, uint256 amount) external onlyQuestManager {
        if (address(medqToken) == address(0)) revert RewardVault__MedqTokenNotConfigured();
        if (_rewards[questId].totalAmount != 0) revert RewardVault__QuestAlreadyFunded(questId);
        if (amount == 0) revert("RewardVault: zero amount");

        _rewards[questId] = QuestReward({totalAmount: amount, claimedAmount: 0});

        // Mint tokens directly to this vault
        medqToken.mint(address(this), amount);

        emit RewardFunded(questId, amount);
    }

    /**
     * @notice Release reward to a quest completer.
     *         Called by QuestManager when a quest is completed.
     * @param questId ID of the quest
     * @param recipient Address to receive the reward
     * @param amount Amount of MEDQ tokens to release (with 18 decimals)
     */
    function releaseReward(uint256 questId, address recipient, uint256 amount) external onlyQuestManager {
        QuestReward storage reward = _rewards[questId];
        if (reward.totalAmount == 0) revert RewardVault__QuestNotFunded(questId);
        if (amount == 0) revert("RewardVault: zero amount");

        uint256 available = reward.totalAmount - reward.claimedAmount;
        if (amount > available) revert RewardVault__InsufficientBalance(questId, amount, available);

        reward.claimedAmount += amount;

        // Transfer tokens to recipient
        IERC20(address(medqToken)).safeTransfer(recipient, amount);

        emit RewardReleased(questId, recipient, amount);
    }

    function rewardInfo(uint256 questId) external view returns (QuestReward memory) {
        return _rewards[questId];
    }

    function _requireQuestManager() internal view {
        if (msg.sender != questManager) revert RewardVault__OnlyQuestManager();
    }
}
